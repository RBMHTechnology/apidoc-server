plugins {
    id 'java'
    id 'org.springframework.boot' version '1.5.15.RELEASE'
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    jcenter()
}

ext['assertj.version'] = '3.10.0'

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-devtools"
    compile "org.apache.maven.wagon:wagon-http:3.0.0"
    compile "org.apache.httpcomponents:httpclient"
    compile "io.vavr:vavr:0.9.2"
    compile "com.google.guava:guava:${guavaVersion}"

    testCompile "org.assertj:assertj-vavr:0.0.1"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}


task sourcesJar(type: Jar, dependsOn: classes, description: 'Builds a sources jar artifact suitable for maven deployment.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc, description: 'Builds a javadoc jar artifact suitable for maven deployment.') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testJar(type: Jar, dependsOn: testClasses, description: 'Builds a tests jar artifact suitable for maven deployment.') {
    classifier = 'tests'
    from sourceSets.test.output
}

tasks.withType(Jar) {
    manifest {
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor': "Red Bull Media House GmbH"
        attributes 'Build-Date': new Date().format("yyyy-MM-dd HH:mm:ss")
    }
}

bootRun {
    if (project.hasProperty('applicationProperties')) {
        systemProperty 'spring.config.location', "file:${applicationProperties}"
    }
}

wrapper.gradleVersion = '4.9'
